; Copyright (c) uib gmbh (www.uib.de)
; This sourcecode is owned by uib gmbh
; and published under the Terms of the General Public License.
; credits: http://www.opsi.org/en/credits/

include_append "%ScriptPath%\check_nsis_exitcode.opsiscript"

DefVar $found$
DefVar $UninstallProgram$

Set $found$     = "false"

Message "uninstalling (old) %installingProdName% ..."

if ($SetupType$ = "nsis")
   Set $UninstallProgram$ = $InstallDir$ + "\uninstall.exe"
   if (not($InstallDir$ = '')) and FileExists($UninstallProgram$)
		comment "Uninstall program found, starting uninstall"
		killtask "QfinderPro.exe"
		Winbatch_uninstall_nsis /WaitforProcessending "Au_.exe" /Timeoutseconds 10
		sub_check_exitcode
		comment "include custom post deinstall file"
		DefVar $CustomPostDeinstall$
		set $CustomPostDeinstall$ = getProductProperty("custom-post-deinstall","none")
		if not ($CustomPostDeinstall$ = "none")
			if FileExists("%ScriptPath%\custom\" + $CustomPostDeinstall$)
		               include_insert "%ScriptPath%\custom\" + $CustomPostDeinstall$
			endif
		endif
	endif
endif

if (not($InstallDir$ = '')) and (not($InstallDir$ = "not used"))
   Files_uninstall
endif

;-----------------------------------------------------
[Winbatch_uninstall_nsis]
"$UninstallProgram$" /S

[Files_uninstall]
del -sf "$InstallDir$\"
